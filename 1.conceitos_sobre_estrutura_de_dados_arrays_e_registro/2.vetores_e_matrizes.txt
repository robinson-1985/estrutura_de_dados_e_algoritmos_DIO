                     VETORES (ARRAYS) E MATRIZES.

Vetores e Matrizes ou arrays são estruturas de dados simples que podem auxiliar quando 
há muitas variáveis do mesmo tipo em um algoritmo.

https://portugol-webstudio.cubos.io/ide

Vetores ou array uni-dimensional é uma variaǘel que armazena várias vriáveis do mesmo 
tipo. 

O vetor é uma estrutura de dados indexada, que pode armazenar uma determinada quantidade 
de valores do mesmo tipo. 

Vamos ver abaixo alguns exemplos de algoritmo escrito em portugol. Utilizamos para isto 
o site do link acima:

Exemplo 1: 

programa {
	funcao inicio() {
		inteiro numeros[] = {39, 45, 54, 55}
		
		para (inteiro posicao = 0; posicao <= 3; posicao++){
		    escreva(numeros[posicao], " ")
		}
	}
}


Exemplo 2:

programa
{
	funcao inicio() 
	{ 
		inteiro vetor[] = { 1, 3, 5, 7, 9}  // Cria o vetor com valores pré-definidos
		inteiro numero
		logico achou = falso  // Variável para armazenar o resultado da procura

		escreva ("Qual número deseja procurar? ")
		leia (numero)

		para (inteiro posicao = 0; posicao < 5; posicao++)
		{
			se (vetor[posicao] == numero)
			{
				escreva ("Encontrado na posição: ", posicao, "\n")
				achou = verdadeiro  
			}
		}
		
		se (nao achou) 
		{
			escreva ("O número não está no vetor\n")
		}
	}
}

Exemplo 3: - utilizando strings (cadeia)

programa
{
	funcao inicio()
	{
		// Criação dos vetores, já com os dados inicializados
		cadeia nome[] = { "Andre", "Thiago" , "Bruno", "Carlos", "Cassio" }
		real altura[] = { 1.71, 1.78, 1.75, 1.87, 1.71 }

		// Cria o cabeçalho da tabela
		escreva ("--------------------\n")
		escreva ("       TABELA       \n")
		escreva ("--------------------\n")

		/* Perocorre os vetores exibindo as informações. Note que as informações
		 * são relacionadas, colocando-as na mesmas na mesma posição em ambos os 
		 * vetores. Por exemplo, se quiséssemos incluir a idade de cada pessoa,
		 * criaríamos mais um vetor do tipo inteiro e a idade de Andre, seria 
		 * armazenada na posição 0 do novo vetor, a idade de Thiago, na posição 1, 
		 * a de Bruno na posição 2, e assim por diante.
		 */		  
		para (inteiro posicao = 0; posicao < 5; posicao++)
		{
			// O caracter especial \t serve para escrever uma tabulação
			escreva (nome[posicao], "\t\t", altura [posicao], "\n")
		}
	}
}

                        MATRIZ

Matriz ou array multi-dimensional é um vetor de vetores.

Uma matriz é um vetor que possui duas ou mais dimensões.

Exemplo 1:

programa
{
	inclua biblioteca Util --> u
	
	funcao inicio()
	{
		// Define as dimensões (linhas e colunas) da matriz
		const inteiro TAMANHO = 5

		// Cria a matriz
		inteiro matriz[TAMANHO][TAMANHO] 

		para (inteiro linha = 0; linha < TAMANHO; linha++)
		{
			para (inteiro coluna = 0; coluna < TAMANHO; coluna++)
			{
				matriz[linha][coluna] = u.sorteia(1, 9) // Atribui um valor aleatório à posição da matriz
				
				escreva("[", matriz[linha][coluna], "]") // Exibe o valor contido na posição da matriz
			}
			
			escreva ("\n")
		}
	}
}


